buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.6'
    }
}

group 'fi.linuxbox.http'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'com.bmuschko.docker-remote-api'

sourceCompatibility = 1.8

jar.manifest.attributes(
        'Main-Class': 'fi.linuxbox.http.Main'
)

repositories {
    // for docker plugin dependencies; we don't have any of our own...
    mavenCentral()
}

import com.bmuschko.gradle.docker.tasks.image.*

task dockerfile(type: Dockerfile) {
    description 'Creates Dockerfile'
    destFile = file("$buildDir/dockerfile/Dockerfile")

    from 'openjdk:8-jre-alpine'
    maintainer 'Mikko VÃ¤rri "vmj@linuxbox.fi"'
    copyFile "${tasks.jar.archiveName}", '/'
    defaultCommand '/usr/bin/java', '-jar', "/${tasks.jar.archiveName}"
}

task dockerResources(type: Sync) {
    description 'Copies stuff to docker context root'

    from jar, dockerfile
    into "$buildDir/docker/"
}

task dockerImage(type: DockerBuildImage) {
    description 'Creates docker image'
    dependsOn dockerResources

    inputDir = dockerResources.destinationDir
    tag = "vmj0/http-server:java8"
}
